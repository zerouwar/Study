package sprinkler

import cn.chenhuanming.study.drools.sprinkler.domain.Fire
import cn.chenhuanming.study.drools.sprinkler.domain.Sprinkler
import cn.chenhuanming.study.drools.sprinkler.domain.Room
import cn.chenhuanming.study.drools.sprinkler.domain.Alarm

rule "when the room is fire,turn on sprinkler"
when
    Fire($room:room)
    $sprinkler:Sprinkler(room==$room,on==false)
then
    modify($sprinkler){setOn(true)};
    System.out.println("Turn on sprinkler for room"+$room.getName());
end

rule "when thr fire is gone turn off the sprinkler"
when
    $room:Room()
    $sprinkler:Sprinkler(room==$room,on==true)
    not Fire(room==$room)
then
    modify($sprinkler){setOn(false)};
    System.out.println("Turn off sprinkler for room"+$room.getName());
end

rule "raise the alarm when we have one or more fires"
when
    exists Fire()
then
    insert(new Alarm());
    System.out.println("Raise the alarm");
end

rule "Cancel the alarm when all fires have gone"
when
    not Fire()
    $alarm:Alarm()
then
    delete($alarm);
    System.out.println("Cancel the alarm");
end

rule "Status output when things are ok"
when
    not Alarm()
    not Sprinkler( on == true )
then
    System.out.println( "Everything is ok" );
end